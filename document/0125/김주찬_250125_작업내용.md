# 25.01.25

## 1. Jenkins Pipeline Test (2)

##### 1. GitLab Webhooks로 변경 감지 (2)

- 24일날 작업한 것은 커스텀 메세지가 아닌 정해진 틀.
- 직접 정보 받아와서 커스텀 메세지 작성하는 것으로 변경



1. Pipeline Item 생성
   ![Image](https://github.com/user-attachments/assets/e18387e5-9ef7-4a8b-bdf4-dbd90d1b53fb)

2. Triggers 설정
   이전에 한 것과 동일하게 GitLab Push를 감지할 수 있는 Webhook 설정하기.

3. Pipeline 작성

   - 스크립트를 직접 작성할 것이기 때문에 Pipeline script 선택.
   - 만약 원격 레포에 저장해서 관리한다면 from SCM 선택해서 SCM 설정하고 SCM 상의 Jenkinsfile 파일 경로 작성.

   

   1. GitLab 커밋메세지 가져와서 MM 메세지 보내기
      ```groovy
      pipeline {
          agent any
          environment {
              GIT_COMMIT_MESSAGE = ''  // 커밋 메세지 저장할 환경변수
              AUTHOR = ''   // 커밋한 작성자
          }
          stages {
              stage('Checkout Code') {  // 깃랩 체크아웃 실행
                  steps {
                      script {
                          checkout([
                              $class: 'GitSCM',
                              branches: [[name: '*/master']],
                              userRemoteConfigs: [[
                                  url: 'https://lab.ssafy.com/s12-webmobile4-sub1/S12P11E205.git',
                                  credentialsId: 'gitlab-credentials'
                              ]],
                          ])
                      }
                  }
              }
              stage('Fetch Commit Message') {  // 커밋메세지&&작성자 가져오기
                  steps {
                      script {
                          GIT_COMMIT_MESSAGE = sh(  // 가장 최근 깃커밋 기록에서 주제 가져오기
                              script: "git log -1 --pretty=format:%s",
                              returnStdout: true
                          ).trim()
                          echo "Fetched Commit Message: ${GIT_COMMIT_MESSAGE}"
      
                          AUTHOR = sh(  // 가장 최근 커밋 기록에서 저자 가져오기
                              script: "git log -1 --pretty=format:%an",
                              returnStdout: true
                          ).trim()
                          echo "Commit Author: ${AUTHOR}"
                      }
                  }
              }
              stage('Notify Mattermost') {  //Mattermost로 알림 메세지 보내기
                  steps {
                      script {
                          def message = "Gitlab_Check - #${env.BUILD_NUMBER} Changes:\n" +
                              "- ${GIT_COMMIT_MESSAGE} [${AUTHOR}]"
                          
                          mattermostSend color: 'good', message: message
                      }
                  }
              }
          }
          post {
              always {
                  echo "Pipeline completed."
              }
          }
      }
      ```

       

   2. 타임스탬프 추가

      ```groovy
      pipeline {
          agent any
          environment {
              ...
              TIMESTAMP = ''  // 환경변수 생성
          }
          stages {
              stage('Checkout Code')...
              stage('Fetch Commit Message and Timestamp') {
                  steps {
                      script {
                          ...
                          
                          TIMESTAMP = sh(  // 커밋에서 날짜 가져오기 - 지정된 포맷으로
                              script: "git log -1 --pretty=format:%ad --date=format:'%Y-%m-%d %H:%M:%S'",
                              returnStdout: true
                          ).trim()
                          echo "Timestamp: ${TIMESTAMP}"
                      }
                  }
              }
              stage('Notify Mattermost') {
                  steps {
                      script {
                          def message = "Gitlab_Check - #${env.BUILD_NUMBER} Changes:\n" +
                              "- ${GIT_COMMIT_MESSAGE} [${AUTHOR}]\n\n" +
                              "`${TIMESTAMP}`" // 바로 출력하면 mm의 이모지가 있는 경우 치환되어 버려서 코드블럭으로 감싸기
                          
                          mattermostSend color: 'good', message: message
                      }
                  }
              }
          }
          ...
      ```

      - 이모지랑 치환되버린 타임스탬프

       <img src="https://github.com/user-attachments/assets/11d4c534-9a55-4a8d-a005-10ef9515b67c" alt="Image" style="zoom:50%;" />

   3. Jira 이슈키 추출하고 링크 넣어서 보내기

      ```groovy
      pipeline {
          agent any
          environment {
              ...
              ISSUE_KEY = ''
              JIRA_LINK = ''
              CLEANED_MESSAGE = ''
              JIRA_BASE_URL = 'https://ssafy.atlassian.net'
          }
          stages {
              stage('Checkout Code')...
              stage('Fetch Commit Message and Timestamp')...
              stage('Extract Jira Issue Key') {  // 이슈키 추출
                  steps {
                      script {
                          def issueKeyPattern = /\[#(S12P11E205-\d+)]/    //이슈키 패턴 설정
                          def matcher = GIT_COMMIT_MESSAGE =~ issueKeyPattern
                          // 패턴에 맞는 문자열 추출
                          if (matcher) {  // 추출된 키 있다면
                              ISSUE_KEY = matcher[0][1]
                              JIRA_LINK = "${JIRA_BASE_URL}/jira/software/c/projects/S12P11E205/boards/7717?selectedIssue=${ISSUE_KEY}"
              
                              CLEANED_MESSAGE = GIT_COMMIT_MESSAGE.replaceFirst(issueKeyPattern, '').trim()  // 커밋메세지에서 이슈키 제거해서 저장
                          } else {
                              echo "No Jira Issue Key found in the commit message."
                              CLEANED_MESSAGE = GIT_COMMIT_MESSAGE
                              // 이슈키가 없다면 메세지 그대로 사용
                          }
                      }
                  }
              }
              stage('Notify Mattermost') {
                  steps {
                      script {
                          def message = "Gitlab_Check - #${env.BUILD_NUMBER} Changes:\n" +
                              (ISSUE_KEY ? "  - [[#${ISSUE_KEY}](${JIRA_LINK})] " : "") +  // 이슈키가 추출된 경우엔 이슈키에 Jira링크 연결
                              "${CLEANED_MESSAGE} [${AUTHOR}]\n\n\n" +
                              "`${TIMESTAMP}`"
                          
                          mattermostSend color: 'good', message: message
                      }
                  }
              }
          }
          ...
      ```

       

   

   <img src="https://github.com/user-attachments/assets/c31f67cb-658a-40f4-b618-ad5b2d61cb5c" alt="Image" style="zoom:67%;" />

