# 25.02.03

## 1. Jenkins Pipeline (8)

### 1. Build 자동화

##### 1. 환경 변수 세팅

```groovy
// jira 댓글 달 때 json 형식으로 body 변환하기 위함
import groovy.json.JsonOutput

pipeline {
    agent any
    // 사용 도구 선언 - 젠킨스 툴 설정에서 잡아놓은거
    tools {
        gradle 'Default Gradle'
        jdk 'Zulu17'
        git 'Default Git'
    }
    environment {
        // 프로젝트 구조 변수
        ROOT = "GitLab_Build"
        BACKEND_DIR = "Back"
        DOCKERFILE_PATH = "Dockerfile"
        STAGE_NAME = ''
        
        // Docker 설정
        DOCKER_USER = 'rocatrun'
        IMAGE_NAME = 'rocatrun'
        GIT_COMMIT_SHORT = ''
        DOCKER_TAG = ''
        
        // 깃 정보
        COMMIT_MSG = ''
        COMMIT_HASH = ''
        AUTHOR = ''
        ERROR_MSG = "false"
        
        // 서버 정보
        TEST_SERVER = '34.22.83.248'
        PROD_SERVER = 'i12e205.p.ssafy.io'
        BRANCH_NAME = ''
        JIRA_BASE_URL = 'https://ssafy.atlassian.net'
        GITLAB_BASE_URL = 'https://lab.ssafy.com/s12-webmobile4-sub1/S12P11E205'
    }
    stages {
        ...
    }
	post ...
}
```



##### 2. 깃랩 클론/체크아웃

```groovy
stage('Checkout and Update') {
    steps {
        script {
            STAGE_NAME = "Checkout and Update (1/9)"
            def repoExists = fileExists('.git')
            if (repoExists) {
                echo "Repository exists. Updating..."
                try {
                    checkout([
                        $class: 'GitSCM',
                        branches: [[name: '*/develop']],
                        userRemoteConfigs: [[
                            url: "${GITLAB_BASE_URL}.git",
                            credentialsId: 'gitlab-credentials'
                        ]],
                        extensions: [
                            [$class: 'CleanBeforeCheckout'],
                            [$class: 'PruneStaleBranch']
                        ]
                    ])
                    withCredentials([gitUsernamePassword(credentialsId: 'gitlab-credentials', gitToolName: 'git-tool')]) {
                        sh """
                            git fetch --all --prune
                            git remote update --prune
                            git gc --prune=now
                        """

                        BRANCH_NAME = sh(script: """
                            git ls-remote --sort=-committerdate origin 'refs/heads/*' |
                            awk -F'/' '{print substr(\$0, index(\$0,\$3))}' |
                            head -n 1
                        """, returnStdout: true).trim()
                        echo "Latest branch: ${BRANCH_NAME}"

                        sh "git checkout -B ${BRANCH_NAME} origin/${BRANCH_NAME} --force"

                        GIT_COMMIT_SHORT = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                        DOCKER_TAG = "${env.BUILD_NUMBER}-${GIT_COMMIT_SHORT}"
                        echo DOCKER_TAG
                    }
                } catch (Exception e) {
                    echo "Error during update: ${e.message}"
                    ERROR_MSG = "Failed to update repository"
                    error ERROR_MSG
                }
            } else {
                echo "Repository does not exist. Cloning..."
                try {
                    withCredentials([gitUsernamePassword(credentialsId: 'gitlab-credentials', gitToolName: 'git-tool')]) {
                        sh "git clone ${GITLAB_BASE_URL}.git ."
                        sh "git checkout develop"

                        BRANCH_NAME = sh(script: """
                            git ls-remote --sort=-committerdate origin 'refs/heads/*' |
                            awk -F'/' '{print substr(\$0, index(\$0,\$3))}' |
                            head -n 1
                        """, returnStdout: true).trim()
                        echo "Latest branch: ${BRANCH_NAME}"

                        sh "git checkout -B ${BRANCH_NAME} origin/${BRANCH_NAME} --force"

                        GIT_COMMIT_SHORT = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                        DOCKER_TAG = "${env.BUILD_NUMBER}-${GIT_COMMIT_SHORT}"
                        echo DOCKER_TAG
                    }
                } catch (Exception e) {
                    echo "Error during clone: ${e.message}"
                    ERROR_MSG = "Failed to clone repository"
                    error ERROR_MSG
                }
            }
            AUTHOR = sh(script: "git log -1 --pretty=format:%an", returnStdout: true).trim()
            COMMIT_MSG = sh(script: 'git log -1 --pretty=%B', returnStdout: true).trim()
            COMMIT_HASH = sh(script: "git log -1 --pretty=format:%H", returnStdout: true).trim()
        }
        script {
            if (sh(
                script: "git ls-tree -d origin/${BRANCH_NAME} Back", 
                returnStatus: true
            ) != 0) {
                currentBuild.result = 'ABORTED'
                ERROR_MSG = "Back 디렉토리가 원격 브랜치에 존재하지 않음"
                error ERROR_MSG
            }

            if (!fileExists("Back")) {
                ERROR_MSG = "Back 디렉토리가 로컬에 존재하지 않음"
                error ERROR_MSG
            }
        }
    }
}
```



##### 3. yml 적용

```groovy
stage('Inject Config') {
    steps {
        script {
            STAGE_NAME = "Inject Config (2/9)"
        }
        withCredentials([file(credentialsId: 'spring-config', variable: 'CONFIG_FILE')]) {
            sh """
                mkdir -p ${BACKEND_DIR}/src/main/resources
                cp \$CONFIG_FILE ${BACKEND_DIR}/src/main/resources/application.yml
            """
        }
    }
}
```





##### 4. gradle 빌드

```groovy
stage('Build') {
    steps {
        script {
            STAGE_NAME = "Build (3/9)"
        }
        dir(BACKEND_DIR) {
            sh "chmod +x gradlew"
            script{
                try {
                    sh "./gradlew clean build -x test"
                } catch(Exception e) {
                    ERROR_MSG = e.getMessage()
                    error ERROR_MSG
                }
            }
        }
    }
    post {
        failure {
            cleanWs()
            script {
                ERROR_MSG += "\nBuild failed"
                error ERROR_MSG
            }
        }
    }
}
```



##### 5. 도커 이미지 빌드

```groovy
stage('Docker Build') {
    steps {
        script {
            STAGE_NAME = "Docker Build (4/9)"
            IMAGE_NAME += (BRANCH_NAME == 'develop') ? "-dev" : "-sub"
            def jarFile = sh(script: "ls Back/build/libs/*.jar", returnStdout: true).trim()
            try {
                docker.build("${DOCKER_USER}/${IMAGE_NAME}:${DOCKER_TAG}", "--no-cache --build-arg JAR_FILE=${jarFile} .")
            } catch(Exception e) {
                ERROR_MSG = e.getMessage()
                error ERROR_MSG
            }
        }
    }
    post {
        failure {
            cleanWs()
            script {
                ERROR_MSG += "\nDocker Build failed"
                error ERROR_MSG
            }
        }
    }
}
```



##### 6. 테스트 서버로 배포

```groovy
stage('Deploy to Test') {
    steps {
        script {
            STAGE_NAME = "Deploy to Test (5/9)"
            def currentBranch = sh(script: 'git rev-parse --abbrev-ref HEAD', returnStdout: true).trim()
            sshagent(['gcloud-ssafy']) {
                sh """
                    set -e
                    docker save ${DOCKER_USER}/${IMAGE_NAME}:${DOCKER_TAG} | gzip > image.tar.gz
                    rsync -av --progress -W image.tar.gz plaksharp@${TEST_SERVER}:/home/plaksharp/

                    sleep 1

                    local_size=\$(stat -c%s image.tar.gz)
                    remote_size=\$(ssh -o StrictHostKeyChecking=no plaksharp@${TEST_SERVER} "stat -c%s image.tar.gz")

                    if [ "\$local_size" -ne "\$remote_size" ]; then
                        echo "ERROR: File size mismatch (Local: \$local_size, Remote: \$remote_size)"
                        exit 1
                    fi

                    sleep 1

                    ssh -o StrictHostKeyChecking=no plaksharp@${TEST_SERVER} "
                        set -e
                        gunzip -c image.tar.gz | docker load
                        docker stop backend || true
                        docker rm backend || true
                        docker run -d --network host --name backend ${DOCKER_USER}/${IMAGE_NAME}:${DOCKER_TAG}
                        rm image.tar.gz
                    "
                """
            }
            echo "Success Test deployment."
        }
    }
    post {
        failure {
            cleanWs()
            script {
                ERROR_MSG = "Test deployment failed"
                error ERROR_MSG
            }
        }
    }
}
```



##### 7. 테스트 서버 헬스체크

```groovy
stage('Test Health Check') {
    steps {
        script {
            STAGE_NAME = "Test Health Check (6/9)"
            def maxRetries = 5
            def timeout = 10
            def success = false

            for (int i = 0; i < maxRetries; i++) {
                sleep(timeout)
                try {
                    def response = httpRequest "http://${TEST_SERVER}:8080/actuator/health"
                    if (response.status == 200) {
                        success = true
                        break
                    }
                } catch(e) {
                    echo "Health check attempt ${i+1} failed"
                }
            }
            if (!success) {
                ERROR_MSG = "Health check failed after ${maxRetries} attempts"
                error ERROR_MSG
            } else {
                docker.withRegistry('https://index.docker.io/v1/', 'docker-credentials') {
                    docker.image("${DOCKER_USER}/${IMAGE_NAME}:${DOCKER_TAG}").push()
                }
            }
        }
    }
}
```



##### 8. 배포 서버로 배포

```groovy
stage('Deploy to Prod') {
    steps {
        script {
            STAGE_NAME = "Deploy to Prod (7/9)"
            def currentBranch = sh(script: 'git rev-parse --abbrev-ref HEAD', returnStdout: true).trim()
            sshagent(['ec2-ssafy']) {
                sh """
                    ssh -o StrictHostKeyChecking=no ubuntu@${PROD_SERVER} "
                        docker pull ${DOCKER_USER}/${IMAGE_NAME}:${DOCKER_TAG}
                        docker stop backend || true
                        docker rm backend || true
                        docker run -d --network host --name backend ${DOCKER_USER}/${IMAGE_NAME}:${DOCKER_TAG}
                    "
                """
            }
            echo "Success Production deployment."
        }
    }
    post {
        failure {
            script {
                ERROR_MSG = "Production deployment failed"
                error ERROR_MSG
            }
        }
    }
}
```



##### 9. 배포 서버 헬스체크

```groovy
stage('Prod Health Check') {
    steps {
        script {
            STAGE_NAME = "Prod Health Check (8/9)"
            def maxRetries = 5
            def timeout = 10
            def success = false

            for (int i = 0; i < maxRetries; i++) {
                sleep(timeout)
                try {
                    def response = httpRequest "http://${PROD_SERVER}:8080/actuator/health"
                    if (response.status == 200) {
                        success = true
                        break
                    }
                } catch(e) {
                    echo "Health check attempt ${i+1} failed"
                }
            }
            if (!success) {
                ERROR_MSG = "Health check failed after ${maxRetries} attempts"
                error ERROR_MSG
            }
        }
    }
}
stage('Complete') {
    steps {
        script {
            STAGE_NAME = "Complete (9/9)"
        }
    }
}
```



##### 10. 결과 알림

```groovy
post {
    always {
        script {
            def issueKeyPattern = /\[#(S12P11E205-\d+)]/
            def issueKey = (COMMIT_MSG =~ /S12P11E205-\d+/) ? (COMMIT_MSG =~ /S12P11E205-\d+/)[0] : null
            def cleanedMessage = issueKey ? COMMIT_MSG.replaceFirst(issueKeyPattern, '').trim() : COMMIT_MSG
            def jiraLink = issueKey ? "${JIRA_BASE_URL}/jira/software/c/projects/S12P11E205/boards/7717?selectedIssue=${issueKey}" : ''

            def message = "${env.JOB_NAME} - #${env.BUILD_NUMBER}\n" +
                          "- 결과: ${currentBuild.currentResult}\n" +
                          "- 브랜치: ${BRANCH_NAME}\n- 커밋: " +
                          (issueKey ? "[${issueKey}] " : "") +
                          "[${cleanedMessage}](${GITLAB_BASE_URL}/-/commit/${COMMIT_HASH}) (${GIT_COMMIT_SHORT}) [${AUTHOR}]\n" +
                          "- 실행 시간: ${currentBuild.durationString}\n" +
                          "- 최종 실행된 스테이지 : ${STAGE_NAME}\n" +
                          ((ERROR_MSG!="false") ? "- ERROR :\n`${ERROR_MSG}`\n" : "")

            if (issueKey) {
                try {
                    def requestBody = [body: message]
                    def response = httpRequest authentication: 'jira-credentials',
                        contentType: 'APPLICATION_JSON',
                        httpMode: 'POST',
                        requestBody: groovy.json.JsonOutput.toJson(requestBody),
                        url: "${JIRA_BASE_URL}/rest/api/2/issue/${issueKey}/comment"
                    echo "JIRA comment added successfully. Status: ${response.status}"
                } catch(e) {
                    echo "JIRA 코멘트 추가 실패: ${e.message}"
                }
            }

            message += "- 상세: " + (currentBuild.currentResult == 'SUCCESS' ? ":jenkins7:" : ":jenkins5:") + " [Jenkins](${env.BUILD_URL})"
            message += jiraLink ? " | :jira: [Jira](${jiraLink}) " : " | :jira3:"
            message += "\n\n`${env.BUILD_TIMESTAMP}`"

            mattermostSend color: currentBuild.currentResult == 'SUCCESS' ? 'good' : 'danger', message: message
        }
    }
    failure {
        script {
            // def message = "${env.JOB_NAME} - #${env.BUILD_NUMBER} Failed:\n" +
            //     "- 파이프라인 실행 중 오류가 발생했습니다.\n" +
            //     "- 최종 실행된 스테이지 : ${STAGE_NAME}\n\n" +
            //     "`${env.BUILD_TIMESTAMP}`"

            // mattermostSend color: 'danger', message: message

            cleanWs(cleanWhenNotBuilt: false,
                deleteDirs: true,
                disableDeferredWipeout: true,
                notFailBuild: true)
        }
    }
}
```

